# setting up a new file - this is done when link is dropped into the world, not
# at actual file creation.

seasons:
  # Change Link starting point
  07/4193/: db option.startingGroup  # Room
  07/4195/: db option.startingRoom  # Room
  07/4197/: db option.startingPosY  # Y
  07/4199/: db option.startingPosX  # X
  07/41a6/: db option.animalCompanion  # Natzu layout

  # On file creation, set additional flags to skip a few events/cutscenes/tutorials
  # from the vanilla game which are cumbersome in a rando context.
  07/setInitialFlags: |
    # no linked gaming
    xor a
    ld (wIsLinkedGame),a

    # Mark intro as seen
    ld a,0a
    call setGlobalFlag
    ld a,1c
    call setGlobalFlag

    # Animal vars
    ld a,ff
    ld (wAnimalTutorialFlags),a

    # Remove the requirement to go in the screen under Sunken City tree to make Dimitri bullies appear
    ld a,20
    ld (wDimitriState),a

    # room flag 6
    ld a,40
    ld (c796),a  # Remove post-dungeon cutscene at D1 entrance
    ld (c78d),a  # Remove post-dungeon cutscene at D2 entrance
    ld (c760),a  # Remove post-dungeon cutscene at D3 entrance
    ld (c71d),a  # Remove post-dungeon cutscene at D4 entrance
    ld (c78a),a  # Remove post-dungeon cutscene at D5 entrance
    ld (c700),a  # Remove post-dungeon cutscene at D6 entrance
    ld (c7d0),a  # Remove post-dungeon cutscene at D7 entrance
    ld (c800),a  # Remove post-dungeon cutscene at D8 entrance
    ld (c829),a  # Remove Maku Tree cutscene at temple of seasons gate
    ld (c82a),a  # Remove Maku Tree cutscene at winter tower
    ld (c79b),a  # Sokra stump
    ld (c7e9),a  # Sokra in town

    # room flags 4 | 6
    ld a,40
    ld (c7a7),a  # Vanilla start room

    # room flags 3 | 5 | 6 | 7
    ld a,e8
    ld (c79a),a  # Rosa portal

    # room flags 6 | 7
    ld a,c0
    ld (c798),a  # Troupe
    ld (c7cb),a  # Rosa (first encounter)

    # room flag 0
    ld a,01
    ld (c701),a  # Flower/rock tile exists near D6

    # give L-3 ring box
    ld a,10
    ld (c697),a  # treasure flag
    ld a,03
    ld (wRingBoxLevel),a

    # automatically open tarm gate if requirement is 0 jewels
    ld a,option.tarmGateRequiredJewels
    or a
    jr nz,.done
    ld a,80
    ld (c763),a

    .done
    ld hl,4182  # initialFileVariables
    ret
  07/400a/: call setInitialFlags

  # Remove the "Accept our quest, hero!" cutscene when launching the game
  # for the first time
  02/1874/: |
    nop
    nop
    xor a
    cp a,1

  # Disable Impa intro script by jumping directly to @impaIntroDone
  0b/74aa/: db 74,ef
  # Space from 0x2F4AC to 0x2F4EE (included) is free

ages:
  # flags in wGlobalFlags to be set at start of game.
  03/initialGlobalFlags: |
      db 0a,0c,1d,20,23,2b,33,3d,40,41,43,45,ff

  # set flags to skip opening and a bunch of other things. see doc/technical.md
  # for a dictionary of the flags.
  03/setInitialFlags: |
      push hl

      # global flags
      ld hl,initialGlobalFlags
      .loop
      ldi a,(hl)
      cp a,ff
      jr z,.done
      push hl
      call setGlobalFlag
      pop hl
      jr .loop
      .done

      # linked global flags
      ld a,(wIsLinkedGame)
      or a
      jr z,.unlinked
      ld a,38
      push hl
      call setGlobalFlag
      pop hl
      .unlinked

      # animal vars
      ld a,option.animalCompanion
      ld (wAnimalRegion),a
      cp a,0c
      jr nz,.notDimitri
      ld a,02
      ld (c737),a # disable SE nuun portal
      .notDimitri
      ld a,03
      ld (wDimitriState),a
      ld a,ff
      ld (wAnimalTutorialFlags),a

      # maku tree state (vanished)
      ld a,01
      ld (wMakuTreeState),a

      # room flags 3 | 6 | 7
      ld a,c8
      ld (c739),a # big intro cutscene

      # room flag 7
      ld a,80
      ld (c8bb),a # linked crescent island rosa encounter (1)

      # room flags 5 | 6
      ld a,60
      ld (c8cb),a # linked crescent island rosa encounter (2)

      # room flag 6
      ld a,40
      ld (c703),a # d4 entrance (present)
      ld (c70a),a # d5 entrance
      ld (c72e),a # inside graveyard poe's grave
      ld (c73a),a # nayru's house
      ld (c759),a # triangle stone
      ld (c76a),a # first encounter with impa
      ld (c77a),a # start
      ld (c77b),a # graveyard kids
      ld (c77c),a # graveyard poe
      ld (c78d),a # d1 entrance
      ld (c790),a # d7 entrance
      ld (c7ba),a # d3 entrance
      ld (c80f),a # d6 entrance (past)
      ld (c820),a # ralph encounter on talus peaks
      ld (c85c),a # d8 entrance
      ld (c883),a # d2 entrance (past)
      ld (c897),a # ralph encounter near rafton
      ld (c9f6),a # top of black tower, south of stairs

      # room flag 3
      ld a,08
      ld (c725),a # foreman
      ld (c813),a # symmetry city "middle house"

      # room flag 0
      ld a,01
      ld (c738),a # maku tree (present)
      ld (c876),a # black tower entrance

      # give L-3 ring box
      ld a,10
      ld (c69f),a # treasure flag
      ld a,03
      ld (wRingBoxLevel),a

      pop hl
      ret
  03/6e97/: jp setInitialFlags
